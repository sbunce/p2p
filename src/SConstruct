#site_scons
import boost
import gtkmm
import system

#source files for objects to be linked in to p2p_gui
src_gui = [
	'gui.cpp',
	'gui_about.cpp',
	'gui_download_status.cpp',
	'gui_preferences.cpp',
	'main_gui.cpp'
]

#source files for objects to be linked in to p2p_nogui
src_nogui = [
	'main_nogui.cpp'
]

#source files for objects to be linked in to p2p_gui and p2p_nogui
src_common = [
	'CLI_args.cpp',
	'client_buffer.cpp',
	'client.cpp',
	'client_new_connection.cpp',
	'client_server_bridge.cpp',
	'CMWC4096.cpp',
	'database_connection.cpp',
	'database_table_blacklist.cpp',
	'database_table_download.cpp',
	'database_table_hash.cpp',
	'database_table_preferences.cpp',
	'database_table_prime.cpp',
	'database_table_search.cpp',
	'database_table_share.cpp',
	'download.cpp',
	'download_factory.cpp',
	'download_file.cpp',
	'download_hash_tree.cpp',
	'encryption.cpp',
	'hash_tree.cpp',
	'http.cpp',
	'number_generator.cpp',
	'rate_limit.cpp',
	'request_generator.cpp',
	'server_buffer.cpp',
	'server.cpp',
	'server_index.cpp',
	'sha.cpp',
	'slot.cpp',
	'slot_file.cpp',
	'slot_hash_tree.cpp',
	'speed_calculator.cpp',
	'thread_pool.cpp'
]

env = Environment()
system.env_setup(env)
boost.env_setup(env)
env_gui = env.Clone()
gtkmm.env_setup(env_gui)

#export environment to sub scons files
Export('env')
Export('env_gui')

#sub scons files
SConscript([
	'dep_graph/SConstruct',
	'libsqlite3/SConstruct',
	'libtommath/SConstruct',
	'overlay_sim/SConstruct',
	'site_scons/SConstruct',
	'test_manager/SConstruct',
	'unit_tests/SConstruct'
])

static_libs = [
	boost.static_lib('date_time'),
	boost.static_lib('filesystem'),
	boost.static_lib('system'),
	boost.static_lib('thread')
]

print 'building with',env.GetOption('num_jobs'),'threads'

#compile different object groups
src_gui_obj = env_gui.Object(src_gui)
src_nogui_obj = env.Object(src_nogui)
src_common_obj = env.Object(src_common)

#link
env_gui.Program('p2p_gui', src_gui_obj + src_common_obj + static_libs)
env.Program('p2p_nogui', src_nogui_obj + src_common_obj + static_libs)
